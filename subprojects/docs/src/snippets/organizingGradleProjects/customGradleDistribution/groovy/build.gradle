// tag::taskRegistration[]
import org.gradle.distribution.DownloadGradle

// end::taskRegistration[]

// tag::distribution[]
plugins {
    id 'distribution'
}

// end::distribution[]

version = '0.1'

// tag::taskRegistration[]
def downloadGradleDistribution =
        tasks.register("downloadGradleDistribution", DownloadGradle) {
            description = "Downloads the Gradle distribution with a given version."
            gradleVersion = "6.7"
            gradleDistributionType = "bin"
        }
// end::taskRegistration[]

// tag::distribution[]
tasks.named("distZip") {
    dependsOn(downloadGradleDistribution)
}

distributions {
    main {
        version = downloadGradleDistribution.get().getDistributionNameBase().get()
        distributionBaseName = "custom"
        contents { contents ->
            contents.from(zipTree(downloadGradleDistribution.get().destinationFile.get())) {
                eachFile {
                    relativePath =
                            new RelativePath(
                                    true,
                                    *relativePath.segments.findAll {
                                        it != "gradle-${downloadGradleDistribution.get().gradleVersion.get()}"
                                    }
                            )
                }
                includeEmptyDirs = false
            }
        }
    }
}
// end::distribution[]
